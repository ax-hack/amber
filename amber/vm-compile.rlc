;
; rowl - 1st generation
; Copyright (C) 2010 nineties
;
; $Id: vm-compile.rlc 2015-06-21 18:52:57 nineties $
;

; a compiler for compiling virtual machine code

(import "stdlib")
(import "config")

; === utilities ===
(define not_reachable args (do
    (eprintln "not reachable here: " (car args) )
    (exit 1)
    ))
(define not_implemented args (do
    (var msgs (cdr args))
    (eprintln "not implemented '" (car args) "' : "  . msgs)
    ))

(define dispatch (callbacks default args) (do
    (if (cons? (car args))
        (do
            (var head (caar args))
            (var fun (assoc head callbacks))
            (if fun
                (fun . args)
                (default . args)
            )
        )
        (default . args)
    )))

(var label_id 0)
(define mklabel (sym) (do (incr label_id) (tosym (++ sym '.' (- label_id 1)))))
(define print_label (sym) (print sym ":\n"))

(define escape (c)
    (cond
        ((== c '\n') "\\n")
        ((== c '\0') "\\0")
        ((== c '\a') "\\a")
        ((== c '\b') "\\b")
        ((== c '\f') "\\f")
        ((== c '\r') "\\r")
        ((== c '\t') "\\t")
        ((== c '\v') "\\v")
        (otherwise   c)))

(define emit_string (str) (do
    (print '"')
    (upto i 0 (- (string_len str) 1) (print (escape ([] i str))))
    (print '"')
    ))

(define address? (type)
    (assoc type `((string . @true) (int[] . @true) (byte[] . @true) (char[] . @true) (void*[] . @true)
        )))

(define emit_operand (opd)
    (cond
        ((int? opd)     (print '$' opd))
        ((== opd '\0')  (print "$0"))
        ((char? opd)    (print "$'" (escape opd)))
        ((== `%top opd) (print "(%esp)"))
        ((symbol? opd)  (print opd))
        ((== `var (car opd)) (do
            (var offs (- var_pos (cadr opd)))
            (print (* -4 offs))
            (print '(')
            (emit_operand `%ebp)
            (print ')')
            ))
        ((== `global (car opd)) (do
            (var type (cadr opd))
            (var sym  (caddr opd))
            (if (address? type) (print '$' sym) (print sym))
            ))
        ((== `arg (car opd)) (do
            (print (* 4 (+ (cadr opd) 1)))
            (print '(')
            (emit_operand `%ebp)
            (print ')')
            ))
        ((== `deref (car opd)) (do
            (if (!= (caddr opd) 0) (print (caddr opd)))
            (print '(')
            (emit_operand (cadr opd))
            (print ')')
            ))
        ((== `offset (car opd)) (do
            (print (caddr opd))
            (print "(,")
            (emit_operand (cadr opd))
            (print ',')
            (print (cadddr opd))
            (print ')')
            ))
        ((== `label (car opd)) (do
            (print (cadr opd))
            (if (cddr opd) (do (print '.') (print (caddr opd))))
            ))
        (otherwise (not_implemented "emit_operand" opd))
        ))

(define emit args (do
    (print '\t' (car args))
    (set args (cdr args))
    (if args (print ' '))
    (while args (do
        (emit_operand (car args))
        (if (cdr args) (print ", "))
        (set args (cdr args)))
        )
    (print '\n')))

(define lookup_8breg (reg) (assoc reg `((%eax . %al) (%ebx . %bl) (%ecx . %cl) (%edx . %dl))))
(define lookup_16breg (reg) (assoc reg `((%eax . %ax) (%ebx . %bx) (%ecx . %cx) (%edx . %dx))))
(define drop (len) (emit "addl" (* 4 len) `%esp))
(define movl  (src dst) (if (!= src dst) (emit "movl" src dst)))
(define movzb (src dst)
    (cond
        ((int? src)     (emit "movzb" src (lookup_8breg dst)))
        ((char? src)    (emit "movzb" src (lookup_8breg dst)))
        (otherwise      (emit "movzbl" src dst))
        ))
(define movb (src dst)
    (cond
        ((int? src)     (emit "movb" src (lookup_8breg dst)))
        ((char? src)    (emit "movb" src (lookup_8breg dst)))
        (otherwise      (emit "movsbl" src dst))
        ))
(define movzw (src dst)
    (cond
        ((int? src)     (emit "movzw" src (lookup_16breg dst)))
        ((char? src)    (emit "movzw" src (lookup_16breg dst)))
        (otherwise      (emit "movzwl" src dst))
        ))
(define movw (src dst)
    (cond
        ((int? src)     (emit "movw" src (lookup_16breg dst)))
        ((char? src)    (emit "movw" src (lookup_16breg dst)))
        (otherwise      (emit "movswl" src dst))
        ))

(define pushl (val) (emit "pushl" val))
(define pushb (val) (do (emit "movb" val `%al) (emit "movsbl" `%al `%eax) (pushl `%eax)))
(define popl  (reg) (emit "popl" reg))
(define popb  (reg) (emit "popl" reg))

; === management of variables ===
(var vtable ())
(var vindex 0)
(define vtable_push () (set vtable (cons () vtable)))
(define vtable_pop  () (set vtable (cdr vtable)))

(define lookup_var_helper (sym) (do
    (define loop (sym tab)
        (if (nil? tab) nil (do
            (var v (assoc sym (car tab)))
            (if v v (loop sym (cdr tab)))
            )))
    (loop sym vtable)
    ))

(define lookup_var (sym) (do
    (var v (lookup_var_helper sym))
    (if v v (do (eprintln "undefined variable : " sym) (exit 1)))
    ))

(define variable? (sym) (lookup_var_helper sym))

(define add_var (name opd) (setcar vtable (cons `(@name . @opd) (car vtable))))

(var string_table ())
(define reset_string_table () (set string_table ()))
(define register_string (str) (do
    (var lbl (mklabel `str))
    (set string_table (cons `(@lbl @str) string_table))
    lbl
    ))

; === compilation of expressions ===

(var insn_table
    `((+ . "addl") (- . "subl") (* . "imul") (& . "andl") (| . "orl") (^ . "xorl") (neg . "negl") (not . "notl")))

(define lookup_insn (op) (assoc op insn_table))

(define emit_return ((_ . arg) _ _) (do
    (if arg (emit_expr (car arg) `%eax nil))
    (emit "leave")
    (emit "ret")
    ))

(define emit_label ((_ name . id) _ ret) (do
    (print name)
    (if id (print '.' (car id)))
    (print ":\n")
    (if ret (emit_return `(return) nil nil))
    ))

(define emit_directjump (opd) (emit "jmp" opd))
(define emit_indirectjump (opd) (do
    (emit_expr opd)
    (movl `%top `%eax)
    (emit "jmp" `*%eax)
    ))

(define emit_goto ((_ opd) _ _)
    (cond
        ((and (symbol? opd) (not (variable? opd))) (emit_directjump opd))
        ((and (cons? opd) (== `label (car opd)))   (emit_directjump opd))
        (otherwise (emit_indirectjump opd))
        ))

(var break_points ())
(define push_break_point () (set break_points (cons (mklabel `break) break_points)))
(define pop_break_point  () (set break_points (cdr break_points)))
(define break_point      () (car break_points))
(define emit_break_point () (print_label (break_point)))

(define emit_break ((_) _ _) (emit "jmp" (break_point)))

(define emit_expr_default (expr dst ret) (do
    (if (or dst (cons? expr))
        (cond
            ((int? expr)  (movl expr dst))
            ((char? expr) (movb expr dst))
            ((symbol? expr) (movl (lookup_var expr) dst))
            ((string? expr) (do
                (var sym (register_string expr))
                (movl `(global string @sym) dst)
                ))
            ((and (cons? expr) (== `register (car expr))) (movl (cadr expr) dst))
            ((cons? expr) (emit_call `(call . @expr) dst nil))
            (otherwise (not_implemented "emit_expr_default" expr))
        ))
    (if ret (emit_return `(return) nil nil))
    ))

(define emit_subs ((_ ptr idx) dst ret) (do
    (if dst (do
        (emit_expr ptr `%eax nil)
        (if (!= idx 0) (do
            (pushl `%eax)
            (emit_expr idx `%ecx nil)
            (popl `%eax)
            (emit "sall" 2 %ecx)
            (emit "addl" %ecx %eax)
            ))
        (movl `(deref %eax 0) dst)
        ))
    (if ret (emit_return `(return) nil nil))
    ))

(define emit_subs8 ((_ ptr idx) dst ret) (do
    (if dst (do
        (emit_expr ptr `%eax nil)
        (if (!= idx 0) (do
            (pushl `%eax)
            (emit_expr idx `%ecx nil)
            (popl `%eax)
            (emit "addl" %ecx %eax)
            ))
        (movb `(deref %eax 0) dst)
        ))
    (if ret (emit_return `(return) nil nil))
    ))

(define emit_subsu8 ((_ ptr idx) dst ret) (do
    (if dst (do
        (emit_expr ptr `%eax nil)
        (if (!= idx 0) (do
            (pushl `%eax)
            (emit_expr idx `%ecx nil)
            (popl `%eax)
            (emit "addl" %ecx %eax)
            ))
        (movzb `(deref %eax 0) dst)
        ))
    (if ret (emit_return `(return) nil nil))
    ))

(define emit_subs16 ((_ ptr idx) dst ret) (do
    (if dst (do
        (emit_expr ptr `%eax nil)
        (if (!= idx 0) (do
            (pushl `%eax)
            (emit_expr idx `%ecx nil)
            (popl `%eax)
            (emit "addl" %ecx %eax)
            ))
        (movw `(deref %eax 0) dst)
        ))
    (if ret (emit_return `(return) nil nil))
    ))

(define emit_subsu16 ((_ ptr idx) dst ret) (do
    (if dst (do
        (emit_expr ptr `%eax nil)
        (if (!= idx 0) (do
            (pushl `%eax)
            (emit_expr idx `%ecx nil)
            (popl `%eax)
            (emit "addl" %ecx %eax)
            ))
        (movzw `(deref %eax 0) dst)
        ))
    (if ret (emit_return `(return) nil nil))
    ))


(define emit_deref8 ((_ ptr) dst ret) (do
    (if dst (do
        (emit_expr ptr `%eax nil)
        (movb `(deref %eax 0) dst)
        ))
    (if ret (emit_return `(return) nil nil))
    ))

(define emit_unexpr ((op arg) dst ret) (do
    (var insn (lookup_insn op))
    (if dst
        (do
            (emit_expr arg dst nil)
            (emit insn dst)
        )
        (emit_expr arg nil nil)
        )
    (if ret (emit_return `(return) nil nil))
    ))

(define emit_binexpr ((op lhs rhs) dst ret) (do
    (var insn (lookup_insn op))
    (if dst
        (do
            (emit_expr lhs `%eax nil)
            (pushl `%eax)
            (emit_expr rhs `%ecx nil)
            (popl %eax)
            (emit insn `%ecx `%eax)
            (movl `%eax dst)
        )
        (do
            (emit_expr lhs nil nil)
            (emit_expr rhs nil nil)
        ))
    (if ret (emit_return `(return) nil nil))
    ))

(define emit_div ((_ lhs rhs) dst ret) (do
    (if dst
        (do
            (emit_expr lhs `%eax nil)
            (pushl `%eax)
            (emit_expr rhs `%ecx nil)
            (popl `%eax)
            (emit "xorl" `%edx `%edx)
            (emit "idiv" `%ecx)
            (movl `%eax dst)
            (emit "xorl" `%edx `%edx)
        )
        (do
            (emit_expr lhs nil nil)
            (emit_expr rhs nil nil)
        ))
    (if ret (emit_return `(return) nil nil))
    ))

(define emit_mod ((_ lhs rhs) dst ret) (do
    (if dst
        (do
            (emit_expr lhs `%eax nil)
            (pushl `%eax)
            (emit_expr rhs `%ecx nil)
            (popl `%eax)
            (emit "xorl" `%edx `%edx)
            (emit "idiv" `%ecx)
            (movl `%edx dst)
            (emit "xorl" `%edx `%edx)
        )
        (do
            (emit_expr lhs nil nil)
            (emit_expr rhs nil nil)
        ))
    (if ret (emit_return `(return) nil nil))
    ))

(define emit_shift ((op lhs rhs) dst ret) (do
    (var insn (assoc op `((<< . "shll") (>> . "shrl"))))
    (if dst
        (do
            (emit_expr lhs `%eax nil)
            (pushl `%eax)
            (emit_expr rhs `%ecx nil)
            (popl `%eax)
            (emit insn `%cl `%eax)
            (movl `%eax dst)
        ))
    (if ret (emit_return `(return) nil nil))
    ))

(define emit_comparison ((op lhs rhs)) (do
    (var insn (assoc op `((== . "sete") (!= . "setne") (< . "setl") (> . "setg") (<= . "setle") (>= . "setge") (u< . "setb") (u> . "seta") (u<= . "setbe") (u>= . "setae"))))
    (emit_expr lhs)
    (emit_expr rhs)
    (popl `%eax)
    (popl `%ecx)
    (emit "cmpl" `%ecx `%eax)
    (emit insn `%al)
    (emit "movzbl" `%al `%eax)
    (pushl `%eax)
    ))

(define emit_syscall ((syscall . args) dst ret) (do
    (var regs `(%eax %ebx %ecx %edx %esi %edi %ebp))
    (var narg (length args))
    ; evaculate special purpose registers
    (if (> narg 1) (pushl `%ebx))
    (upto r 4 (- narg 1) (pushl (nth r regs)))
    (foreach e args (do
        (emit_expr e `%eax nil)
        (pushl `%eax)
        ))
    (upto r 1 narg (movl `(deref %esp @(* 4 (- narg r))) (nth (- r 1) regs)))
    (drop narg)
    (emit "int" 0x80)
    (if dst (movl `%eax dst))
    ; restore special purpose registers
    (downto r (- narg 1) 4 (popl (nth r regs)))
    (if (> narg 1) (popl `%ebx))


    (if ret (emit_return `(return) nil nil))
    ))

(define emit_call ((_ func . args) dst ret) (do
    (emit_directcall func args dst)
    (if ret (emit_return `(return) nil nil))
    ))
(define emit_indcall ((_ func . args) dst ret) (do
    (emit_indirectcall func args dst)
    (if ret (emit_return `(return) nil nil))
    ))

(define emit_directcall (func args dst) (do
    (var narg (length args))
    (foreach e (reverse args) (do
        (emit_expr e `%eax nil)
        (pushl `%eax)
        ))
    (emit "call" `(label @func))
    (if (> narg 0) (emit "addl" (* 4 narg) `%esp))
    (if dst (movl `%eax dst))
    ))

(define emit_indirectcall (func args dst) (do
    (var narg (length args))
    (foreach e (reverse args) (do
        (emit_expr e `%eax nil)
        (pushl `%eax)
        ))
    (emit_expr func `%eax nil)
    (emit "call *%eax")
    (if (> narg 0) (emit "addl" (* 4 narg) `%esp))
    (if dst (movl `%eax dst))
    ))

(define emit_def ((type sym . init) dst ret) (do
    (var v (lookup_var sym))
    (if init (do (emit_expr (car init) `%eax nil) (movl `%eax v)))
    (if dst (movl v dst))
    (if ret (emit_return `(return) nil nil))
    ))

(define emit_nop ((_nop_) _ ret) (if ret (emit_return `(return) nil nil)))

(define emit_incrdecr ((inst v) dst ret) (do
    (emit (if (== inst `incr) "addl $1," "subl $1,") (lookup_var v))
    (if dst (movl (lookup_var v) dst))
    (if ret (emit_return `(return) nil nil))
    ))

(define emit_addressof ((_ v) dst ret) (do
    (if dst (emit "leal" (lookup_var v) dst))
    (if ret (emit_return `(return) nil nil))
    ))

(define emit_asgn ((_ v e) dst ret) (do
    (emit_expr e `%eax nil)
    (movl `%eax (lookup_var v))
    (if dst (movl `%eax dst))
    (if ret (emit_return `(return) nil nil))
    ))

(define emit_opasgn ((op v e) dst ret) (do
    (var bop (assoc op `((+= . +) (-= . -) (*= . *) (/= . /) (%= . %) (&= . &) (|= . |) (^= . ^)
        (<<= . <<) (>>= . >>))))
    (emit_expr `(= @v (@bop @v @e)) dst ret)
    ))

(define emit_arrasgn ((_ arr idx e) dst ret) (do
    (emit_expr arr `%eax nil)
    (pushl `%eax)
    (emit_expr idx `%eax nil)
    (emit "sall" 2 `%eax)
    (emit "addl" `%eax `%top)
    (emit_expr e `%eax nil)
    (popl `%ecx)
    (movl `%eax `(deref %ecx 0))
    (if dst (movl `%eax dst))
    (if ret (emit_return `(return) nil nil))
    ))

(define emit_arrasgn8 ((_ arr idx e) dst ret) (do
    (emit_expr arr `%eax nil)
    (pushl `%eax)
    (emit_expr idx `%eax nil)
    (emit "addl" `%eax `%top)
    (emit_expr e `%eax nil)
    (popl `%ecx)
    (emit "movb" `%al `(deref %ecx 0))
    (if dst (movl `%eax dst))
    (if ret (emit_return `(return) nil nil))
    ))

(define comparison? (op) (member? op `(== != < > <= >= u< u> u<= u>=)))

(define emit_if ((_ expr ifthen . ifelse) dst ret) (do
    (set ifelse (if ifelse (car ifelse) nil))
    (cond
        ((and (cons? expr) (== `! (car expr)))  (emit_unless (cadr expr) ifthen ifelse dst ret))
        ((and (cons? expr) (== `&& (car expr))) (emit_seqand_if (cadr expr) (caddr expr) ifthen ifelse dst ret))
        ((and (cons? expr) (== `|| (car expr))) (emit_seqor_if (cadr expr) (caddr expr) ifthen ifelse dst ret))
        ((and (cons? expr) (comparison? (car expr))) (emit_cond_if expr ifthen ifelse dst ret))
        (otherwise
            (do
                (var ifend-lbl (mklabel `end))
                (var ifelse-lbl (if ifelse (mklabel `ifelse) nil))
                (emit_expr expr `%eax nil)
                (emit "testl" `%eax `%eax)
                (emit "jz" (if ifelse-lbl ifelse-lbl ifend-lbl))
                (emit_block ifthen ret)
                (if ifelse-lbl (do (emit "jmp" ifend-lbl) (print_label ifelse-lbl) (emit_block ifelse ret)))
                (print_label ifend-lbl)
            )
        ))
    ))

(define emit_unless (expr ifthen ifelse _ ret)
    (if ifelse
        (emit_expr `(if @expr @ifelse @ifthen) nil ret)
        (emit_expr `(if @expr ((nop)) @ifthen) nil ret)
        ))

(define emit_seqand_if (expr1 expr2 ifthen ifelse _ ret)
    (if ifelse
        (do
            (var ifend-lbl (mklabel `end))
            (emit_expr `(if @expr1 ((if @expr2 @(append ifthen `((goto @ifend-lbl)))))) nil nil)
            (emit_block ifelse ret)
            (print_label ifend-lbl)
            (if ret (emit_return `(return) nil nil))
        )
        (emit_expr `(if @expr1 ((if @expr2 @ifthen))) nil ret)
        ))

(define emit_seqor_if (expr1 expr2 ifthen ifelse _ ret)
    (if ifelse
        (emit_expr `(if (&& (! @expr1) (! @expr2)) @ifelse @ifthen) nil ret)
        (do
            (var ifend-lbl (mklabel `end))
            (emit_expr `(if (&& (! @expr1) (! @expr2)) ((goto @ifend-lbl)) @ifthen) nil nil)
            (print_label ifend-lbl)
            (if ret (emit_return `(return) nil nil))
        )
        ))

(define emit_cond_if (expr ifthen ifelse dst ret) (do
    (var insn (assoc (car expr) `((== . "jne") (!= . "je") (< . "jge") (> . "jle") (<= . "jg") (>= . "jl") (u< . "jae") (u> . "jbe") (u<= . "ja") (u>= . "jb"))))
    (var ifend-lbl (mklabel `end))
    (var ifelse-lbl (if ifelse (mklabel `ifelse) nil))
    (emit_expr (cadr expr) `%eax nil)  ; lhs
    (pushl `%eax)
    (emit_expr (caddr expr) `%eax nil) ; rhs
    (popl `%ecx)
    (emit "cmpl" `%eax `%ecx)
    (emit insn (if ifelse-lbl ifelse-lbl ifend-lbl))
    (emit_block ifthen ret)
    (if ifelse-lbl (do (if (nil? ret) (emit "jmp" ifend-lbl)) (print_label ifelse-lbl) (emit_block ifelse ret)))
    (print_label ifend-lbl)
    (if ret (emit_return `(return) nil nil))
    ))

(define emit_while ((_ expr body) _ ret) (do
    (var loop-lbl (mklabel `loop))
    (print_label loop-lbl)
    (push_break_point)
    (emit_if `(if @expr @(append body `((goto @loop-lbl)))) nil nil)
    (emit_break_point)
    (pop_break_point)
    (if ret (emit_return `(return) nil nil))
    ))

(define emit_for ((_ v a b body) _ ret) (do
    (var loop-lbl (mklabel `loop))
    (emit_expr `(int @v @a) nil nil)
    (print_label loop-lbl)
    (push_break_point)
    (emit_if `(if (< @v @b) @(append body `((incr @v) (goto @loop-lbl)))) nil nil)
    (emit_break_point)
    (pop_break_point)
    (if ret (emit_return `(return) nil nil))
    ))

(define emit_rfor ((_ v a b body) _ ret) (do
    (var loop-lbl (mklabel `loop))
    (emit_expr `(int @v (- @b 1)) nil nil)
    (print_label loop-lbl)
    (push_break_point)
    (emit_if `(if (>= @v @a) @(append body `((decr @v) (goto @loop-lbl)))) nil nil)
    (emit_break_point)
    (pop_break_point)
    (if ret (emit_return `(return) nil nil))
    ))

(define emit_bsr ((_ expr) dst ret) (do
    (if dst
    (do
        (emit_expr expr `%eax nil)
        (emit "bsr" `%eax `%eax)
        (emit "cmovz" `%edx `%eax)
        (movl `%eax dst)
    )
    (emit_expr expr nil nil)
    )
    (if ret (emit_return `(return) nil nil))
    ))

(define emit_asm ((_ . args) _ ret) (do
    (print '\t')
    (foreach a args (if (symbol? a) (emit_operand (lookup_var a)) (print a)))
    (print '\n')
    (if ret (emit_return `(return) nil nil))
    ))

(define emit_dputs ((_ msg) _ ret) (do
    (println "\tpushl %eax")
    (println "\tpushl %ebx")
    (println "\tpushl %ecx")
    (println "\tpushl %edx")
    (println "\tpushl %esi")
    (println "\tpushl %edi")
    (emit_call `(call debug @msg) nil nil)
    (println "\tpopl %edi")
    (println "\tpopl %esi")
    (println "\tpopl %edx")
    (println "\tpopl %ecx")
    (println "\tpopl %ebx")
    (println "\tpopl %eax")
    (if ret (emit_return `(return) nil nil))
    ))

(var expr_callbacks `(
    (nop     . @emit_nop)
    (syscall . @emit_syscall)
    (call    . @emit_call)
    (indcall . @emit_indcall)
    (return  . @emit_return)
    (label   . @emit_label)
    (goto    . @emit_goto)
    (break   . @emit_break)
    (if      . @emit_if)
    (while   . @emit_while)
    (for     . @emit_for)
    (rfor    . @emit_rfor)
    (int     . @emit_def)
    (char    . @emit_def)
    (byte    . @emit_def)
    (void*   . @emit_def)
    (get     . @emit_subs)
    (get8    . @emit_subs8)
    (getu8   . @emit_subsu8)
    (get16   . @emit_subs16)
    (getu16  . @emit_subsu16)
    ([]      . @emit_subs)
    ([]8     . @emit_subs8)
    ([]16    . @emit_subs16)
    (*8      . @emit_deref8)
    (neg     . @emit_unexpr)
    (not     . @emit_unexpr)
    (+       . @emit_binexpr)
    (-       . @emit_binexpr)
    (*       . @emit_binexpr)
    (/       . @emit_div)
    (%       . @emit_mod)
    (&       . @emit_binexpr)
    (|       . @emit_binexpr)
    (^       . @emit_binexpr)
    (<<      . @emit_shift)
    (>>      . @emit_shift)
    (==      . @emit_comparison)
    (!=      . @emit_comparison)
    (<       . @emit_comparison)
    (>       . @emit_comparison)
    (<=      . @emit_comparison)
    (>=      . @emit_comparison)
    (u<      . @emit_comparison)
    (u>      . @emit_comparison)
    (u<=     . @emit_comparison)
    (u>=     . @emit_comparison)
    (=       . @emit_asgn)
    (+=      . @emit_opasgn)
    (-=      . @emit_opasgn)
    (*=      . @emit_opasgn)
    (/=      . @emit_opasgn)
    (%=      . @emit_opasgn)
    (&=      . @emit_opasgn)
    (|=      . @emit_opasgn)
    (^=      . @emit_opasgn)
    (<<=     . @emit_opasgn)
    (>>=     . @emit_opasgn)
    (set     . @emit_arrasgn)
    ([]=     . @emit_arrasgn)
    (set8    . @emit_arrasgn8)
    ([]=8    . @emit_arrasgn8)
    (incr    . @emit_incrdecr)
    (decr    . @emit_incrdecr)
    (address . @emit_addressof)
    (bsr     . @emit_bsr)
    (asm     . @emit_asm)
    (dputs   . @emit_dputs)
    ))

(define emit_expr args (dispatch expr_callbacks emit_expr_default args))

(define emit_block (block ret)
    (if (nil? block)
        (if ret (emit_return `(return) nil nil))
        (foreach e block (emit_expr e nil (and ret last?)))
        ))

(define add_new_expr (sym func) (set expr_callbacks (cons `(@sym . @func) expr_callbacks)))

; === compilation of external declarations ===

(var current_area `text)
(define emit_area (sym)
    (println (assoc sym `((text . ".section .text") (data . ".section .data") (rodata . ".section .rodata")))))
(define change_area (sym) (if (!= sym current_area) (do (emit_area sym) (set current_area sym))))

(define emit_export ((_ name)) (print ".global " name '\n'))

(define emit_extern ((_ type name)) (add_var name `(global @type @name)))

(define emit_static_scalar ((type sym . init)) (do
    (change_area `data)
    (print_label sym)
    (print "\t.long ")
    (if init
        (println (car init))
        (println 0))
    (add_var sym `(global @type @sym))
    ))

(define emit_static_string ((_ sym str)) (do
    (change_area `rodata)
    (print_label sym)
    (print "\t.string ")
    (emit_string str)
    (print '\n')
    (add_var sym `(global string @sym))
    ))

(define emit_static_array ((type len sym . init)) (do
    (change_area `data)
    (var elemsz (assoc type `((int[] . 4) (void*[] . 4) (char[] . 1) (byte[] . 1))))
    (if init (do
            (println sym ':')
            (var directive (if (== elemsz 4) "\t.long " "\t.byte "))
            (foreach v (car init) (println directive v))
            (var rem (- len (length (car init))))
            (if (> rem 0) (println "\t.zero " (* elemsz rem)))
        )
        (do
            (println ".comm " sym ", " (* len elemsz))
            (add_var sym `(global @type @sym))
        )
    )
    (add_var sym `(global @type @sym))
    ))

(var var_pos 0)
(define scan_defvar ((type v . init)) (do
    (var sz (assoc type `((int . 1) (char . 1) (byte . 1) (int* . 1) (void* . 1))))
    (add_var v `(var @var_pos @sz))
    (set var_pos (+ var_pos sz))
    (if init (scan_expr init))
    ))

(define scan_if ((_ _ ifthen . ifelse)) (do
    (foreach e ifthen (scan_expr `(@e)))
    (if ifelse (foreach e (car ifelse) (scan_expr `(@e))))
    ))

(define scan_for ((_ v _ _ body)) (do
    (add_var v `(var @var_pos 1))
    (set var_pos (+ var_pos 1))
    (foreach e body (scan_expr `(@e)))
    ))

(define scan_while ((_ _ body)) (foreach e body (scan_expr `(@e))))

(define ignore (code) ())
(define scan_expr (e) (dispatch scan_function_callbacks ignore e))

(define scan_function (body) (do
    (set var_pos 0)
    (foreach e body (scan_expr `(@e)))
    ))

(var scan_function_callbacks `(
    (int   . @scan_defvar)
    (char  . @scan_defvar)
    (byte  . @scan_defvar)
    (int*  . @scan_defvar)
    (void* . @scan_defvar)
    (if    . @scan_if)
    (while . @scan_while)
    (for   . @scan_for)
    (rfor  . @scan_for)
    ))

(define emit_function ((_ name params body)) (do
    (vtable_push)
    (reset_string_table)

    ; register parameters
    (var idx 1)
    (foreach p params (do (add_var p `(arg @idx)) (incr idx)))
    ; register variables
    (scan_function body)

    (change_area `text)
    (print_label name)
    (pushl `%ebp)
    (movl `%esp `%ebp)

    ; allocate stack frame
    (if (> var_pos 0) (emit "subl" (* 4 var_pos) `%esp))

    (emit_block body true)

    (vtable_pop)

    (foreach ent string_table (emit_static_string `(string @(car ent) @(cadr ent))))

    ))

(var extdecl_callbacks `(
    (export  . @emit_export)
    (extern  . @emit_extern)
    (int     . @emit_static_scalar)
    (void*   . @emit_static_scalar)
    (int*    . @emit_static_scalar)
    (byte*   . @emit_static_scalar)
    (string  . @emit_static_string)
    (int[]   . @emit_static_array)
    (byte[]  . @emit_static_array)
    (char[]  . @emit_static_array)
    (void*[] . @emit_static_array)
    (fun     . @emit_function)
    ))

(define compile (prog) (do
    (foreach decl prog (dispatch extdecl_callbacks not_reachable `(@decl)))
    ))
